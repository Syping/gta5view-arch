# Maintainer: Syping <aur@syping.de>
pkgname=gta5view-qt6
pkgver=1.10.0
pkgrel=1
epoch=
pkgdesc="Open Source Snapmatic and Savegame viewer/editor for GTA V"
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url="https://gta5view.syping.de/"
license=('GPL')
depends=(qt6-base qt6-svg qt6-translations)
makedepends=(cmake gcc git make qt6-base qt6-svg qt6-tools qt6-translations)
provides=("${pkgname%-qt6}")
conflicts=("${pkgname%-qt6}")
source=("https://github.com/SyDevTeam/${pkgname%-qt6}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=("2231afbebbf4a603a214ece08c1d1ef3b7a3d5e998e4165caf2682e7517ce58b")

build() {
    mkdir -p "${pkgname%-qt6}-$pkgver.build"
    cd "${pkgname%-qt6}-$pkgver.build"
    cmake "-GUnix Makefiles" \
          "-DCMAKE_BUILD_TYPE=Release" \
          "-DCMAKE_INSTALL_PREFIX=/usr" \
          "-DGTA5VIEW_BUILDTYPE=Release" \
          "-DQCONF_BUILD=ON" \
          "-DFORCE_QT_VERSION=6" \
          "../${pkgname%-qt6}-$pkgver"
    make
}

package() {
    cd "${pkgname%-qt6}-$pkgver.build"
    make DESTDIR="$pkgdir/" install
}
